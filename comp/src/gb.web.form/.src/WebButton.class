' Gambas class file

''' This class implements a push button.

Export
Inherits WebControl

Public Const _Properties As String = "*,Border=True,Text,Image{WebImage},Immediate"
'Public Const _DrawWith As String '= "Button"
Public Const _DefaultEvent As String = "Click"
Public Const _DefaultSize As String = "16,4"

'' This event is raised when the button is pressed.
''
'' ### See also
'' [../immediate]
Event Click

'' Return or set the button text.
Property Text As String
'' Return or set the button image relative path.
Property Image As String
'' If the Immediate property is set, the [../click] event is raised to generate javascript code that will be executed on the client side when
'' the button is clicked.
''
'' In that case, the Click event handler should only call methods that generate javascript on the client side:
'' - [../../webform/exec](WebForm.Exec)
'' - [../../webtextbox/copy](WebTextBox.Copy)
'' - ...
Property Immediate As Boolean

Private $sText As String
Private $sImage As String
Private $bImmediate As Boolean

'' Create a new WebButton control.

Public Sub _new()
  
End


Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  If $sText = Value Then Return
  $sText = Value
  Me._SetProperty("Text", Value)

End

Private Function Image_Read() As String

  Return $sImage

End

Private Sub Image_Write(Value As String)

  If $sImage = Value Then Return
  $sImage = Value
  Me._SetProperty("Image", Value)

End

Public Sub _BeforeRender()
  
  Dim vSave As Variant
  
  Print "<button"; Me._GetClassId(); 
  If $bImmediate Then
    Print " onclick=\"";
    vSave = WebForm._BeginJavascript()
    Raise Click
    WebForm._EndJavascript(vSave)
    Print "\"";
  Else
    Print Me._GetEventJS("onclick", "click");
  Endif
  Me._RenderStyleSheet()
  If Not Me.Enabled Then Print " disabled";
  Print ">";
  
End


Public Sub _Render()
  
  If $sImage Then 
    Print "<div><img src=\""; Me._GetImageLink($sImage); "\"";
    If $sText Then Print " class=\"gw-button-image\"";
    If Me.Height Then
      Print " style=\"max-height:"; Me.Height; "\"";
    Else
      Print " style=\"height:1em;\"";
    Endif
    ' If Me.Width Or If Me.Height Then
    '   Print " style=\"";
    '   If Me.Width Then Print "max-width:"; Me.Width; ";"
    '   If Me.Height Then Print "max-height:"; Me.Height; ";"
    '   Print "\"";
    ' Endif
    Print ">";
  Endif
  If $sText Then Print "<span>"; Html($sText); "</span>";
  
End

Public Sub _AfterRender()
  
  Raise Render
  Print "</button>"
  
End

'' Click on the button.

Public Sub Click()
  
  Object.Raise(Me, "Click")
  
End

Private Function Immediate_Read() As Boolean

  Return $bImmediate

End

Private Sub Immediate_Write(Value As Boolean)

  If $bImmediate = Value Then Return
  $bImmediate = Value
  Me._SetProperty("Immediate", Value)

End
