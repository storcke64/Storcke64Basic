' Gambas class file

''' This control displays a piece of text.
'''
''' [[ info 
''' @{since 3.16}
''' 
''' This control has been rewritten in Gambas since 3.16 version.
''' ]]

Export
Inherits UserControl

Public Const _Properties As String = "*,Padding{Range:0;64},AutoResize,Alignment{Align.*}=Normal,Border{Border.None;Plain;Sunken;Raised;Etched},Text"
Public Const _DefaultEvent As String = "MouseDown"
Public Const _DefaultSize As String = "8,3"
Public Const _IsContainer As Boolean = False
Public Const _Group As String = "Form"
Public Const _Similar As String = "Label"

'' Return or set the text alignment.
''
'' See the [../../align] class for a list of constants to use with this property.

Property Alignment As Integer

'' Return or set the border displayed around the control.
''
'' See the [../../border] class for a list of constants to use with this property.

Property Border As Integer Use $iBorder

'' Return or set the displayed text.
Property Text, Caption As String Use $sText

'' Return or set the inner padding around the text, in pixels.
Property Padding As Integer Use $iPadding

'' Return or set if the control adapts its size to its contents automatically.
Property AutoResize As Boolean Use $bAutoResize

'' Return or set if the control background is transparent. This property is deprecated.
Property Transparent As Boolean Use $bTransparent

Private $iAlign As Integer = Align.Normal
Private $hExt As RectF
Private $bWarningTransparent As Boolean
Private $bRichText As Boolean

Public _Wrap As Boolean
Private $bLocked As Boolean

'' Create a new Label.

Public Sub _new()
  
  $bRichText = Me Is TextLabel
  If $bRichText Then 
    _Wrap = True
    $iAlign = Align.TopNormal
  Endif
  
End

Private Sub Alignment_Write(Value As Integer)

  If $iAlign = Value Then Return
  $iAlign = Value
  Me.Refresh

End

Private Sub Border_Write(Value As Integer)
  
  If $iBorder = Value Then Return
  $iBorder = Value
  _UpdateSize
  Me.Refresh
  
End

Private Sub Text_Write(Value As String)
  
  If $sText = Value Then Return
  $sText = Value
  _UpdateSize
  Me.Refresh
  
End

Private Sub Padding_Write(Value As Integer)

  If $iPadding = Value Then Return
  $iPadding = Value
  _UpdateSize
  Me.Refresh

End

Private Sub GetPadding() As Integer

  Dim P As Integer

  P = $iPadding
  Select Case $iBorder
    Case Border.Plain
      Inc P
    Case Border.Raised, Border.Sunken
      P += Style.FrameWidth + 1
  End Select
    
  Return P  

End

Public Sub _UpdateSize()
  
  Dim W As Integer
  Dim H As Integer
  Dim P As Integer
  Dim hRect As Rect
  
  $hExt = Null
  If $bLocked Then Return
  If Me.Design Then Return
  If Not $sText Then Return
  If Not $bAutoResize Then Return
  
  $bLocked = True

  P = GetPadding()

  If $bRichText Then
    If _Wrap Then
      hRect = Me.Font.RichTextSize($sText, Me.W - P * 2)
    Else
      hRect = Me.Font.RichTextSize($sText)
    Endif
  Else
    hRect = Me.Font.TextSize($sText)
  Endif
  
  W = hRect.W + P * 2
  H = hRect.H + P * 2
  
  If Align.IsMiddle($iAlign) And If Not $bRichText Then
    If H < Me.H Then H = Me.H
  Endif
  
  Me.Resize(W, H)
  
  $bLocked = False
  
End

Private Sub AutoResize_Write(Value As Boolean)

  If $bAutoResize = Value Then Return
  $bAutoResize = Value
  _UpdateSize

End

Public Sub UserControl_Font()

  _UpdateSize
  Me.Refresh
  
End

Private Sub GetExtents() As RectF

  Dim X As Float

  ' Check Paint.Scalable to workaround a QT bug with Paint.TextExtents() on such fonts

  If Not $hExt Then
    If $bRichText Then
      If _Wrap Then
        $hExt = Paint.RichTextSize($sText, Me.W - GetPadding() * 2)
      Else
        $hExt = Paint.RichTextSize($sText)
      Endif
      If Paint.Font.Scalable Then X = Paint.RichTextExtents($sText).X
    Else
      $hExt = Paint.TextSize($sText)
      If Paint.Font.Scalable Then X = Paint.TextExtents(LTrim($sText)).X
    Endif
    
    'Debug Me.Name; ": "; Paint.Font.ToString();; $hExt.W;; "[";; X;; "]"
    $hExt.X = X
    
  Endif
  
  Return $hExt

End

Public Sub UserControl_Draw()
  
  Dim P As Integer
  Dim F As Integer
  Dim hExt As RectF
  Dim W As Float
  Dim X As Float
  
  If $iBorder Then
    If $iBorder = Border.Plain Then
      F = 1
    Else
      F = Style.FrameWidth + 1
    Endif
    
    Paint.Save
    Paint.Rectangle(0, 0, Paint.W, F)
    Paint.Rectangle(0, Paint.H - F, Paint.W, F)
    Paint.Rectangle(0, 0, F, Paint.H)
    Paint.Rectangle(Paint.W - F, 0, F, Paint.H)
    Paint.Clip()
    Style.PaintPanel(0, 0, Paint.W, Paint.H, $iBorder)
    Paint.Restore
    Paint.Rectangle(F, F, Paint.W - F * 2, Paint.H - F * 2)
    Paint.Clip()
  Endif
  
  If Not $sText Then Return
  
  hExt = GetExtents()

  P = GetPadding()

  If Align.IsLeft($iAlign) Then
    X = P - hExt.X
  Else If Align.IsRight($iAlign) Then
    X = Me.W - hExt.W - hExt.X - P
  Else
    X = P - Int(hExt.X + 1)
  Endif

  W = Me.W - X - P - hExt.X
  
  'If Not _Wrap Then
    If Align.IsLeft($iAlign) Then
      Inc W
    Else If Align.IsRight($iAlign) Then
      Dec X
      Inc W
    Else
      Inc W
    Endif
  'Endif
  
  If Not Me.Enabled Then 
    Paint.Background = Color.Merge(Style.BackgroundOf(Me), Style.ForegroundOf(Me))
  Else 
    Paint.Background = Style.ForegroundOf(Me)
  Endif

  If $bRichText Then
    Paint.DrawRichText($sText, X, P, W, Me.H - P * 2, $iAlign)
  Else
    Paint.DrawText($sText, X, P, W, Me.H - P * 2, $iAlign)
  Endif
  
End

Private Function Alignment_Read() As Integer

  Return $iAlign

End

Private Sub Transparent_Write(Value As Boolean)

  If Not $bWarningTransparent Then
    Error "gb.gui: warning; Label.Transparent and TextLabel.Transparent are deprecated"
    $bWarningTransparent = True
  Endif
  
  $bTransparent = Value

End

'' Resize the control so that it adapts to its contents.

Public Sub Adjust()

  Dim bSave As Boolean

  bSave = $bAutoResize
  $bAutoResize = True  
  _UpdateSize
  $bAutoResize = bSave
  
End

Public Sub UserControl_Resize()

  _UpdateSize()
  
End
