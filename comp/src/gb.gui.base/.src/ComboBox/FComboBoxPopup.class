' Gambas class file

Private Const MAX_ITEMS As Integer = 10
Private $aList As String[]

Public Sub Open(aList As String[], iIndex As Integer, hCtrl As Control) As Integer
  
  Dim iRow As Integer
  Dim I As Integer
  Dim hFont As Font
  Dim WM As Integer
  Dim X, Y, W, H As Integer
  Dim hScreen As Screen
  
  hScreen = Screens[hCtrl.Window.Screen]
  
  $aList = aList
  
  Me.Arrangement = Arrange.None
  
  gvwPopup.Rows.Count = aList.Count
  gvwPopup.Columns.Count = 1
  'gvwPopup.Columns[0].Expand = True
  gvwPopup.Columns[0].W = -1
  
  W = hCtrl.W - Style.FrameWidth * 2
  X = hCtrl.ScreenX + Style.FrameWidth
  
  WM = gvwPopup.Columns[0].W + 2
  If $aList.Count > MAX_ITEMS Then WM += Style.ScrollbarSize
  
  W = Max(W, WM) + 2
  H = (gvwPopup.Font.TextHeight(" ") + 6) * Min(MAX_ITEMS, $aList.Count) + 2
  
  Me.Arrangement = Arrange.Fill
  
  W = Min(W, hScreen.AvailableWidth)
  
  Me.Resize(W, H)
  
  If hCtrl.ScreenY + hCtrl.H + H > hScreen.AvailableHeight Then 
    Y = hCtrl.ScreenY - H
  Else 
    Y = hCtrl.ScreenY + hCtrl.H
  Endif
  
  If X + W > hScreen.AvailableWidth Then X = hScreen.AvailableWidth - W
  If X < 0 Then X = 0
  
  Try gvwPopup.Row = iIndex
  
  iRow = Me.ShowPopup(X, Y) - 1
  
  $aList = Null
  
  Return iRow
  
End

Public Sub gvwPopup_Data(Row As Integer, (Column) As Integer)

  gvwPopup.Data.Text = $aList[Row]

End

Public Sub gvwPopup_MouseMove()

  Dim iRow As Integer

  If Mouse.X < 0 Or If Mouse.X >= gvwPopup.ClientW Then Return
  
  iRow = gvwPopup.RowAt(Mouse.Y)
  If iRow >= 0 Then 
    gvwPopup.Rows[iRow].EnsureVisible
    gvwPopup.Row = iRow
  Endif

End

' Public Sub gvwPopup_MouseWheel()
'   
'   gvwPopup_MouseMove
'   
' End

Public Sub gvwPopup_MouseUp()
  
  Me.Close(gvwPopup.Row + 1)
  
End

Public Sub Form_Activate()

  gvwPopup.SetFocus

End

Public Sub Form_KeyPress()

  If Key.Code = Key.Return Or If Key.Code = Key.Enter Then
    Me.Close(gvwPopup.Row + 1)
  Else If Key.Code = Key.Escape Then
    Me.Close
  Endif

End
