' Gambas class file

Export

Static Private $cNameFromKey As Collection

Static Public Sub _call(Text As String, Shortcut As String, Optional AddIfMissing As Boolean) As String

  Dim iPos As Integer
  
  ' iPos = String.InStr(Text, "&")
  ' If iPos Then 
  '   Shortcut = String.Mid$(Text, iPos + 1, 1)
  '   Text = String.Left(Text, iPos - 1) & String.Mid$(Text, iPos + 1)
  ' Endif
  
  If Not Shortcut Then Return Text
  
  iPos = InStr(Text, Shortcut, 1, gb.IgnoreCase)
  If iPos = 0 Then 
    If AddIfMissing Then Text &= " (&" & String.UCase(Shortcut) & ")"
    Return Text
  Else
    Return Left(Text, iPos - 1) & "&" & Mid$(Text, iPos)
  Endif
  
End

Static Public Sub FromKey() As String

  Dim sShortcut As String
  Dim sSymbol As String
  Dim hClass As Class
  Dim sName As String
  
  If Key.Control And If Key.Code <> Key.ControlKey Then sShortcut &= "Ctrl+"
  If Key.Shift And If Key.Code <> Key.ShiftKey Then sShortcut &= "Shift+"
  If Key.Alt And If Key.Code <> Key.AltKey Then sShortcut &= "Alt+"
  If Key.Meta And If Key.Code <> Key.MetaKey Then sShortcut &= "Meta+"
  
  If Not $cNameFromKey Then
  
    $cNameFromKey = New Collection
    hClass = Classes["Key"]
    For Each sSymbol In hClass.Symbols
      With hClass[sSymbol]
        If .Kind = Class.Constant Then $cNameFromKey[.Value] = .Name
      End With
    Next
  
  Endif
  
  sName = $cNameFromKey[Key.Code]
  If sName Then
    Return sShortcut & sName
  Else
    Return sShortcut & String.Chr(Key.Code)
  Endif
  
End
