' Gambas class file

Export

Inherits UserControl

Property Text As String

'' Return or set the title of the icon panel as rich text.
Property RichText As String
Property Picture As Picture
Property Read Container As Container
Property Visible As Boolean

Private $sText As String
Private $hPict As Picture
Private $bRichText As Boolean

Private Sub GetIconPanel() As IconPanel
  
  Return Me.Parent.Parent.Parent
  
End

Private Sub GetView() As GridView
  
  Return GetIconPanel()._GetView()
  
End

Private Sub GetIndex() As Integer
  
  Return GetIconPanel()._GetIndex(Me)
  
End


Public Sub _new()

End


Private Function Text_Read() As String

  If Not $bRichText Then Return $sText
  'Try Return GetView()[GetIndex(), 0].Text

End

Private Sub Text_Write(sText As String)

  $sText = sText
  $bRichText = False
  GetIconPanel()._UpdateIconViewWidth
  GetView()[GetIndex(), 0].Refresh

End

Private Function RichText_Read() As String

  If $bRichText Then Return $sText
  'Try Return GetView()[GetIndex(), 0].Text

End

Private Sub RichText_Write(sText As String)

  $sText = sText
  $bRichText = True
  GetIconPanel()._UpdateIconViewWidth
  GetView()[GetIndex(), 0].Refresh

End

Private Function Picture_Read() As Picture

  Return $hPict

End

Private Sub Picture_Write(hPict As Picture)

  $hPict = hPict
  GetIconPanel()._UpdateIconViewWidth
  GetView()[GetIndex(), 0].Refresh

End

Private Function Container_Read() As Container

  Return Me

End

Private Function Visible_Read() As Boolean

  Return GetView().Rows[GetIndex()].H > 0

End

Private Sub Visible_Write(Value As Boolean)

  GetIconPanel()._SetVisible(Me, Value)

End
