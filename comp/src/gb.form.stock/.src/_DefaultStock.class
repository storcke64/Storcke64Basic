' Gambas class file

Export

Class Image
Class SvgImage

Static Private $cSynonymous As New Collection

Static Private $bDebug As Boolean

Static Public Sub _init()

  Dim hFile As File
  Dim sLine As String
  Dim iPos As Integer
  
  hFile = Open "links" For Input
  For Each sLine In hFile.Lines
    sLine = Trim(sLine)
    iPos = InStr(sLine, " -> ")
    If iPos < 2 Then Continue
    $cSynonymous[Trim(Left(sLine, iPos - 1))] = Trim(Mid$(sLine, iPos + 4))
  Next
  
  $bDebug = Env["GB_STOCK_DEBUG"] = "1"
  
End

' Static Private Sub MakeDark(hImage As Image) As Image
'   
'   Dim X As Integer
'   Dim Y As Integer
'   Dim hColor As ColorInfo
'   Dim iLum As Integer
'   Dim iFg As Integer
'   Dim iBg As Integer
'   
'   iFg = Color[Color.Foreground].Luminance
'   iBg = Color[Color.Background].Luminance
'   hImage = hImage.Copy()
'   
'   For X = 0 To hImage.W
'     For Y = 0 To hImage.H
'       hColor = Color[hImage[X, Y]]
'       iLum = hColor.Luminance 
'       hColor.Luminance = iFg - (hColor.Luminance - iBg)
'       hImage[X, Y] = hColor.Color
'     Next
'   Next
'   
'   Return hImage
'   
' End
' 
' 

Static Private Sub Stretch(hImage As Image, iSize As Integer) As Image
  
  If hImage.W = iSize And If hImage.H = iSize Then Return hImage
  Return hImage.Stretch(iSize, iSize)
  
End

Static Private Sub LoadIcon(sKey As String, iSize As Integer, sStock As String) As Image

  Dim sPath As String
  
  sPath = sStock &/ CStr(iSize) &/ sKey
  If System.RightToLeft And If Exist(sPath & "-rtl.png") Then sPath &= "-rtl"
  If $bDebug And If iSize = 32 And If Not Exist(sPath & ".png") Then Error "gb.form.stock: unknown icon: " & sKey
  Return Image.Load(sPath & ".png")
  
End


Static Public Sub LoadImage(sKey As String, iSize As Integer, Optional sStock As String) As Image

  Dim hImage As Image

  If $cSynonymous.Exist(sKey) Then sKey = $cSynonymous[sKey]
  If Not sStock Or If Not IsDir(sStock) Then sStock = "gambas"
  
  'If sKey = "tools" Then Debug sPath
  
  If iSize > 32 Then
    Try hImage = LoadIcon(sKey, 128, sStock)
    If Not Error Then Goto RETURN_IMAGE
  Endif
  
  hImage = LoadIcon(sKey, 32, sStock)
  
RETURN_IMAGE:

  hImage = Stretch(hImage, iSize)
  If Application.DarkTheme And If hImage Then hImage = hImage.Invert(True) 'Color.Background, Color.Foreground)
  Return hImage
  
End

Static Public Sub LoadSvgImage(sKey As String, Optional sStock As String) As SvgImage

  Dim sPath As String
  
  If $cSynonymous.Exist(sKey) Then sKey = $cSynonymous[sKey]
  If Not sStock Or If Not IsDir(sStock) Then sStock = "gambas"
  sPath = sStock &/ "scalable" &/ sKey
  If System.RightToLeft And If Exist(sPath & "-rtl.svg") Then sPath &= "-rtl"
  'If sKey = "tools" Then Debug sPath
  Return SvgImage.Load(sPath & ".svg")
  
End
