' Gambas class file

Private $sPath As String

Public Sub Run(sPath As String) As String
  
  $sPath = sPath
  If Me.ShowModal() Then Return $sPath
  
End


Public Sub Form_Open()

  Settings.Read(Me)
  Settings.Read(fchExec, "fchExec")

  fchExec.Filter = ["*.gambas", ("Gambas applications")]
  fchExec.SelectedPath = $sPath
  fchExec.SetFocus
  
  chkDebug.Value = Project.KeepDebugInfo
  chkCompress.Value = Project.CompressFiles

  chkIncVersion.Visible = Not Project.VersionFile
  panVersion.Visible = chkIncVersion.Visible

  txtMajor.Value = Project.MajorVersion
  txtMinor.Value = Project.MinorVersion
  txtRelease.Value = Project.ReleaseVersion

  chkIncVersion.Value = Not Project.Config["/Executable/DoNotIncrementVersion", False]
  chkSetVersion.Value = False

  chkNoTest.Visible = Project.HasTest()
  chkNoTest.Value = Project.Config["/Executable/NoTestModule", False]

  ' If Project.CreateComponent Then 
  '   chkShortcut.Text = ("Install in the user component directory")
  ' Else
  '   chkShortcut.Text = ("Create a shortcut on the desktop")
  ' Endif
  chkShortcut.Value = Project.CreateShortcut
  chkShortcut.Visible = Project.Type = Project.TYPE_NORMAL
  txtCommand.Text = Project.RunAfterMakingExec
  
  tabExec.Index = 0
  
End

Public Sub Form_Close()

  Settings.Write(fchExec, "fchExec")
  Settings.Write(Me)

End

Public Sub btnOK_Click()

  $sPath = fchExec.SelectedPath
  
  With Project
    .KeepDebugInfo = chkDebug.Value
    .CompressFiles = chkCompress.Value
    .CreateShortcut = chkShortcut.Value
    .RunAfterMakingExec = Trim(txtCommand.Text)
    .Config["/Executable/DoNotIncrementVersion"] = chkIncVersion.Value = CheckBox.False
    .Config["/Executable/NoTestModule"] = CBool(chkNoTest.Value)
    
    If chkSetVersion.Value Then
      .MajorVersion = txtMajor.Value 
      .MinorVersion = txtMinor.Value
      .ReleaseVersion = txtRelease.Value
    Endif
    
    .WriteProject(True)
  End With
  
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkIncVersion_Click()

  If chkIncVersion.Value Then
    txtMajor.Value = Project.MajorVersion
    txtMinor.Value = Project.MinorVersion
    txtRelease.Value = Project.ReleaseVersion + 1
    chkSetVersion.Value = False
  Else
    txtMajor.Value = Project.MajorVersion
    txtMinor.Value = Project.MinorVersion
    txtRelease.Value = Project.ReleaseVersion
  Endif

End

Public Sub chkSetVersion_Click()

  txtMajor.Enabled = chkSetVersion.Value
  txtMinor.Enabled = chkSetVersion.Value
  txtRelease.Enabled = chkSetVersion.Value
  btnUndoVersion.Enabled = True
  
  If chkSetVersion.Value Then 
    chkIncVersion.Value = False
  Endif

End

Public Sub btnUndoVersion_Click()

  txtMajor.Value = Project.MajorVersion
  txtMinor.Value = Project.MinorVersion
  txtRelease.Value = Project.ReleaseVersion

End
