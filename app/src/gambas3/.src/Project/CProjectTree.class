' Gambas class file

Public Const KEY_SOURCE As String = "$S"
Public Const KEY_MISC As String = "$O"
Public Const KEY_PROJECT As String = "$P"
Public Const KEY_PUBLIC As String = "$B"
Public Const KEY_CONNECTION As String = "$D"
Public Const KEY_EXTERN As String = "$X"

Public Sources As Collection

Private $hTree As TreeView
Private $sDir As String
Private $bCurrentProject As Boolean
Private $sSourceDir As String
Private $aInheritance As New String[]
Private $hInfo As CProjectInfo
Private $hObserver As Observer
Private $cFilter As Collection
Private $bHasJustCollapsed As Boolean
Private $bExpandOnMouseUp As Boolean
Private $cExtern As New Collection
Private $sStartup As String

Private $aOtherProjects As New String[]

Public Sub _new(sDir As String)
  
  $sDir = sDir
  $bCurrentProject = $sDir = Project.Dir
  
  If $bCurrentProject Then Project.Tree = Me
  
  $sSourceDir = sDir &/ ".src"
  If Not Exist($sSourceDir) Then $sSourceDir = $sDir
  
  $hInfo = New CProjectInfo(sDir)
  $sStartup = $hInfo.Startup
  
End

Public Sub RefreshInfo()
  
  $hInfo = New CProjectInfo($sDir)
  
End


Public Sub SetView(hTreeView As TreeView)
  
  If $hTree And If $hTree <> hTreeView Then Error.Raise("TreeView already set")
  
  $hTree = hTreeView
  If $bCurrentProject Then Project.ProjectTree = hTreeView
  $hObserver = New Observer(hTreeView) As "TreeView"
  FillTree
  
End

Private Sub AddFile(sDir As String, sFile As String, Optional bAfter As Boolean, Optional bIgnoreInheritance As Boolean) As Boolean
  
  Dim bIgnore As Boolean
  Dim sPath As String
  Dim sKey As String
  Dim bShow As Boolean
  Dim sExt As String
  Dim sParent As String
  Dim hPict As Picture
  Dim bChecked As Boolean
  Dim bDir As Boolean
  Dim sAfter As String
  Dim hModule As CModule
  Dim bSource As Boolean
  Dim bExtern As Boolean
  Dim sTitle As String
  Dim hStat As Stat
  Dim bParentLink As Boolean
  Dim bStartup As Boolean
  Dim sForm As String
  'Dim bCompressed As Boolean

  sPath = sDir &/ sFile
  
  If $cFilter And If Not $cFilter.Exist(sPath) Then Return
  
  sKey = sPath
  sParent = sDir

  If sDir = $sDir Then
    sParent = KEY_MISC
  Else If sDir = $sDir &/ ".hidden" Then 
    sParent = KEY_PROJECT
  Else If sDir = $sDir &/ ".public" Then 
    sParent = KEY_PUBLIC
  Else If sDir = $sSourceDir Then 
    sParent = KEY_SOURCE
  Else If sDir = $sDir &/ ".connection" Then
    sParent = KEY_CONNECTION
  Else If $cExtern.Exist(sPath) Then
    sParent = KEY_EXTERN
    If Not $hTree.Exist(sParent) Then $hTree.Add(sParent, ("Files"), Picture["icon:/small/link"], $sDir)
    bExtern = True
    Try sTitle = Project.Files[sPath].GetTitle()
    If sTitle Then sFile = sTitle
  Endif

  Try hStat = Stat(sPath)
  If Error Then Return
  
  With hStat

    If .Hidden Then Return

    bShow = False
    If Not bExtern Then bSource = IsSourcePath(sDir &/ sFile)

    If IsDir(sPath) Then

      If sFile = "CVS" Or If sFile = "CVSROOT" Then Return

      bDir = True

    Else

      'IF InStr(.Perm.User & .Perm.Group & .Perm.Other, "x") THEN CONTINUE

      sExt = Lower(File.Ext(sFile))
      bChecked = False

      If bSource Then

        If CModule.Ext.Exist(sExt) Then

          'sIcon = IMAGE_DIR &/ sExt & ".png"
          bShow = sDir = $sSourceDir
          bChecked = True
          
          sParent = ""
          
          If sExt = "class" Then
            
            For Each hModule In CModule.All
              sForm = sDir &/ File.BaseName(sFile) & "." & hModule.Key
              If Exist(sForm) Then 
                bIgnore = True
                Break
              Endif          
            Next

            If Not bIgnore Then
              'IF sFile = "ReportContainer.class" THEN STOP
              If Project.UseInheritance Then 
                sParent = FindKey(Project.GetParentClass(sPath))
                If sParent Then bAfter = True
              Endif 
              
              If sParent Then
                If File.Ext(sParent) <> "class" Then 
                  sParent = ""
                Else If Not $hTree.Exist(sParent) Then 
                  If Not bIgnoreInheritance Then
                    $aInheritance.Add(sPath)
                    bIgnore = True
                  Else 
                    sParent = ""
                  Endif
                Else If File.Dir(sParent) <> sDir Then
                  bParentLink = True
                Endif
              Endif
            Endif
          Endif

          If Not sParent Then sParent = sDir
          If sParent = $sSourceDir Then sParent = KEY_SOURCE
          
          If Sources[sFile] <> sPath Then
            bIgnore = True
          Endif
            
          sFile = File.BaseName(sFile)
          bStartup = sFile = $sStartup
          
        Endif

      Else If IsConnectionPath(sDir &/ sFile) Then

        If $hInfo.Components.Exist("gb.db") And If sExt = "connection" Then
          sFile = File.BaseName(sFile)
        Else
          bIgnore = True
        Endif
        
      ' Else
      '   
      '   If $bCurrentProject Then
      '     bCompressed = Project.ShouldCompress(sDir &/ sFile)
      '   Endif

      Endif
      
      If Not bChecked Then
      
        If Right$(sFile) = "~" Then
          bIgnore = True
        Else If sDir = $sDir And If sExt = "gambas" Then
          bIgnore = True
        Endif
      
      Endif
  
    Endif
    
    If sExt = "mine" Or If Left(sExt) = "r" And IsDigit(Mid$(sExt, 2)) Then 
      If Exist(sDir &/ File.BaseName(sPath)) Then 
        bIgnore = True
      Endif
    Endif
    
    If Not $hTree.Exist(sParent) Then bIgnore = True

    If Not bIgnore Then

      hPict = GetFileIcon(sPath,, hStat,, bParentLink)

      If bAfter Then 
        With $hTree
          .MoveTo(sParent)
          .MoveChild
          If Not bDir Then
            While .Available
              If Not IsDir(.Item.Key) Then Break
              sAfter = .Item.Key
              .MoveNext
            Wend
          Endif
          While .Available
            If String.Comp(.Item.Text, sFile, gb.IgnoreCase + gb.Natural) > 0 Then Break
            If bDir And If Not IsDir(.Item.Key) Then Break
            sAfter = .Item.Key
            .MoveNext
          Wend
        End With
      Endif
      
      If $hTree.Exist(sKey) Then
        With $hTree[sKey]
          .Text = sFile
          .Picture = hPict
          .Reparent(sParent)
        End With
      Else
        ' need a diff. Name (Text) for classes to reparent later
        'If sTempName Then sFile = sTempName
        
        $hTree.Add(sKey, sFile, hPict, sParent)
        
        ' 'If a * .class exist reparent the branch
        ' If sExt <> "class" And If $hTree.Exist(File.SetExt(sKey, "class")) Then 
        '   $hTree[File.SetExt(sKey, "class")].Reparent(sKey)
        '   $hTree[File.SetExt(sKey, "class")].Text = sFile
        ' End If
        
        If bDir And If Not bSource Then
          $hTree.Add(sKey & "/", "",, sKey)
          bDir = False
        Endif
      Endif
      
      With $hTree[sKey]
        
        If bStartup Then 
          .Font = $hTree.Font
          .Font.Underline = True
          .Font.Bold = True
        Else
          .Font = Null
        Endif
        
        ' If bCompressed Then
        '   .RichText = Html(.Text) & " â‡£"
        ' Else
        '   .RichText = ""
        ' Endif
        
        If $bCurrentProject Then .Editable = Not Project.IsLocked(sKey)
        If bAfter Then .MoveAfter(sAfter)
        If bShow Then
          $hTree.MoveTo(sKey)
          $hTree.MoveParent
          $hTree.Item.Expanded = True
        Endif
        
      End With
      
      If bSource And If Project.ShowAllClasses Then
        If ExistForm(sFile) Then
        
          sParent = sKey
          sPath = File.SetExt(sPath, "class")
          sKey = File.SetExt(sKey, "class")
          Try hStat = Stat(sPath)
          If Error Then Return
          
          If Not $hTree.Exist(sKey) Then
            With $hTree.Add(sKey, sFile, GetFileIcon(sPath,, hStat,, True), sParent)
              .Font = $hTree.Font
              .Font.Grade -= 2
            End With
          Endif
        
        Endif
      Endif
      
      If $bCurrentProject Then Project.RefreshIcon(sKey)
      
    Endif

  End With  

  Return bDir
  
End

Private Procedure AddDir(Optional sDir As String)

  Dim aDir As New String[]
  Dim sFile As String
  Dim aFile As New String[]

  If sDir Then 
    aDir.Add(sDir)
  Else 
    aDir.Add($sDir)
    If $sDir <> $sSourceDir Then aDir.Add($sSourceDir)
  Endif

  $hTree._Begin()

  Repeat

    sDir = aDir[0]
    aDir.Remove(0)
    
    If Not IsDir(sDir) Then Continue

    aFile.Clear
    
    '$bGetSource = FALSE

    For Each sFile In Dir(sDir, "*")
      If IsDir(sDir &/ sFile) Then aFile.Add("D" & sFile)
    Next
  
    For Each sFile In Dir(sDir, "*")
      If Not IsDir(sDir &/ sFile) Then aFile.Add("F" & sFile)
    Next
  
    aFile.Sort(gb.Natural + gb.IgnoreCase)
  
    $aInheritance.Clear
  
    For Each sFile In aFile
  
      sFile = Mid$(sFile, 2)
      'AddFile(sDir, sFile, True)
      If AddFile(sDir, sFile) Then aDir.Add(sDir &/ sFile)
      
    Next
    
    DoRefreshAfter
    
  Until aDir.Count = 0

  $hTree._End()

End

Private Sub FillTree(Optional bDoNotRefreshCache As Boolean)

  Dim sKey As String
  Dim sDir As String
  Dim sFile As String
  Dim iSize As Integer = Stock.GetSize("small")
  Dim sPath As String
  Dim hStat As Stat
  
  'Dim sKeyReset As String

  '$bDisplayForm = Settings["/DisplayForm"]

  For Each $cExtern
    If Not Exist($cExtern.Key) Then $cExtern.Remove($cExtern.Key)
  Next

  If Not bDoNotRefreshCache Then RefreshSources

  With $hTree

    .Clear()

    sKey = $sDir
    .Add(sKey, File.Name($sDir), Project.GetIcon($sDir, iSize))

    .Add(KEY_PROJECT, ("Project"), Picture["icon:/small/directory"], sKey)
    '.Add(KEY_MODULE, ("Modules"), Picture["icon:/small/directory"], sKey)
    '.Add(KEY_CLASS, ("Classes"), Picture["icon:/small/directory"], sKey)
    .Add(KEY_SOURCE, ("Sources"), Picture["icon:/small/directory"], sKey)
    
    If $hInfo.Components.Exist("gb.db") Then .Add(KEY_CONNECTION, ("Connections"), Picture["icon:/small/directory"], sKey)
    If $hInfo.Components.Exist("gb.web") Then .Add(KEY_PUBLIC, ("Public"), Picture["icon:/small/directory"], sKey)
    .Add(KEY_MISC, ("Data"), Picture["icon:/small/directory"], sKey)

    '$bGetSource = TRUE

    AddDir()
    If Exist($sDir &/ ".hidden") Then AddDir($sDir &/ ".hidden")
    If $hInfo.Components.Exist("gb.db") And If Exist($sDir &/ ".connection") Then AddDir($sDir &/ ".connection")
    If $hInfo.Components.Exist("gb.web") And If Exist($sDir &/ ".public") Then AddDir($sDir &/ ".public")

    For Each sKey In $cExtern
      AddFile(File.Dir(sKey), File.Name(sKey))
    Next
    
    For Each sDir In $aOtherProjects
      
      .Add(sDir, File.Name(sDir), Project.GetIcon(sDir, iSize))
      .Add(sDir &/ ".src", ("Project"), Picture["icon:/small/directory"], sDir)
      
      sDir &/= ".src"
      For Each sFile In RDir(sDir)
        sPath = sDir &/ sFile
        hStat = Stat(sPath)
        If hStat.Type = gb.Directory Then
          .Add(sPath, File.Name(sFile), Picture["icon:/small/directory"], File.Dir(sPath))
        Else If File.Ext(sFile) = "class" Then
          .Add(sPath, File.Name(sFile),, File.Dir(sPath)).Picture = GetFileIcon(sPath,, hStat)
        Endif
      Next
      
    Next

  End With

  With $hTree
    .MoveFirst
    While .Available
      .Item.Expanded = True
      .MoveNext
    Wend
  End With

  'If sKeyReset Then sKey = sKeyReset

  'SelectKey(sKey)

  'DefineStartup(Startup, True)

  ' For Each sFamily In CFamily.List
  '   If Not HasModule(sFamily) Then 
  '     For Each hFile In Files
  '       sPath = Files.Key
  '       If Not IsSourcePath(sPath) Then Continue
  '       If Exist(File.SetExt(sPath, sFamily)) Then
  '         Files.Remove(sPath)
  '         hFile.Delete
  '       Endif
  '     Next
  '   Endif
  ' Next

End

Public Function FindPath(sClass As String) As String

  Dim sPath As String

  sPath = Sources[sClass]
  If sPath Then Return sPath

  sPath = Sources[sClass & ".class"]
  If sPath Then Return sPath

  sPath = Sources[sClass & ".module"]
  If sPath Then Return sPath

  sPath = Sources[sClass & ".test"]
  If sPath Then Return sPath
  
End

Public Sub FindKey(sClass As String) As String

  Dim hModule As CModule
  
  Dim sPath As String

  If Not sClass Then Return

  For Each hModule In CModule.All
    sPath = Sources[sClass & "." & hModule.Key]
    If sPath Then Return sPath
  Next
  
  Return FindPath(sClass)

End

Public Sub RefreshSources()

  Dim sPath As String
  Dim sName As String
  
  Sources = New Collection(gb.IgnoreCase)
  
  For Each sPath In Project.Browse($sSourceDir)
    If CModule.Ext.Exist(File.Ext(sPath)) Then
      sName = File.Name(sPath)
      Sources[sName] = $sSourceDir &/ sPath
    Endif
  Next
  
End

Public Sub CheckDuplicates() As String[]

  Dim sPath As String
  Dim cName As New Collection
  Dim aList As New String[]
  Dim sName As String
  Dim sDup As String
  
  For Each sPath In Project.Browse($sSourceDir)
    sName = File.Name(sPath)
    sDup = cName[sName]
    If Not sDup Then
      cName[sName] = sPath
    Else
      If sDup <> "*" Then
        aList.Add(sDup)
        cName[sName] = "*"
      Endif
      aList.Add(sPath)
    Endif
  Next
  
  If aList.Count Then Return aList
  
End


Private Sub IsConnectionPath(sPath As String) As Boolean
  
  Return sPath Begins $sDir &/ ".connection" &/ "/"
  
End

Private Sub DoRefreshAfter()
  
  Dim sKey As String
  Dim iInd As Integer
  Dim iCount As Integer

  If $aInheritance.Count = 0 Then Return

  Do
    iCount = $aInheritance.Count
    For iInd = 0 To iCount - 1
      sKey = $aInheritance[0]
      $aInheritance.Remove(0)
      AddFile(File.Dir(sKey), File.Name(sKey), True)
    Next
    If $aInheritance.Count >= iCount Then Break
  Loop

  If $aInheritance.Count Then 
    For iInd = 0 To $aInheritance.Max
      sKey = $aInheritance[iInd]
      AddFile(File.Dir(sKey), File.Name(sKey), True, True)
      $aInheritance[iInd] = File.BaseName(sKey)
    Next
    'FMain.ShowError(("The following classes have circular inheritance:") & "\n\n" & $aRefreshAfter.Join(", "))
    $aInheritance.Clear
  Endif

End

Public Sub Refresh(Optional bDoNotRefreshCache As Boolean, cFilter As Collection)
  
  Dim vLayout As Variant
  Dim Y As Integer
  
  $cFilter = cFilter
  vLayout = $hTree.Layout
  Y = $hTree.ScrollY
  FillTree(bDoNotRefreshCache)
  $hTree.Layout = vLayout
  $hTree.ScrollY = Y
  
End

Public Sub GetFileIcon(sPath As String, Optional iSize As Integer, hStat As Stat, bNoEmblem As Boolean, bParentLink As Boolean) As Picture

  Dim hImage As Image
  Dim hPict As Picture
  Dim sKey As String
  Dim sCacheKey As String
  Dim bLink As Boolean
  Dim sIcon As String
  Dim sExt As String
  Dim bStartup As Boolean
  Dim iFileSize As Long
  Dim hFile As File
  Dim sPrefix As String
  'Dim bNoStartup As Boolean
  Dim bConflict As Boolean
  Dim sModule As String
  Dim sLocalPrefix As String
  Dim bLocked As Boolean
  Dim bAdded As Boolean
  Dim hProject As CProjectInfo
  Dim sLink As String
  Dim bExported As Boolean
  Dim bExportedNamespace As Boolean
  Dim fRapport As Float
  Dim bDoNotTranslate As Boolean
  Dim bModified As Boolean
  Dim iEmblemSize As Integer
  Dim bReadOnly As Boolean
  Dim bCompressed As Boolean
  Dim hThumb As Image
  Dim bImage As Boolean
  Dim sExportName As String

  If iSize Then
    sPrefix = "icon:/" & iSize
    sLocalPrefix = "img/" & iSize
  Else
    sPrefix = "icon:/small"
    sLocalPrefix = "img/16"
    iSize = Stock.GetSize("small")
  Endif

  If Not hStat Then 
    Try hStat = Stat(sPath)
    If Error Then Return 
  Endif
  
  With hStat

    bLink = .Type = gb.Link Or bParentLink
    bConflict = False
    If $bCurrentProject Then
      bLocked = Project.IsLocked(sPath)
      bAdded = Project.IsAdded(sPath)
      bDoNotTranslate = Not Project.ShouldTranslate(sPath)
      'bCompressFile = Project.ShouldCompress(sPath)
      bModified = Project.IsModified(sPath)
      bReadOnly = Project.IsReadOnly(sPath)
      bCompressed = Project.ShouldCompress(sPath)
      iEmblemSize = iSize
    Endif
    '$bLastAdded = bAdded

    If IsDir(sPath) Then

      If bLink Then
        sLink = Project.GetAbsoluteLink(sPath, .Link)
        'If Left(sLink) <> "/" Then sLink = sPath &/ sLink
        sLink = File.Dir(sLink)
        hProject = New CProjectInfo(sLink)
        sIcon = hProject.GetIconPath()
        If iSize = 0 Then iSize = 16
      Else
        sIcon = sPrefix &/ "directory"
      Endif

    Else

      'bConflict = VersionControl.InConflict(sPath)
      If $bCurrentProject Then bConflict = Project.IsConflict(sPath)

      sIcon = "" 'sPrefix &/ "file"

      sExt = File.Ext(sPath)
      Select Case sExt

        Case "form", "class", "module", "test", "webpage", "report", "webform", "termform"
          If IsSourcePath(sPath) Or If IsClassFromOtherProject(sPath) Then
            sModule = sExt
            sIcon = sExt
            'bStartup = File.BaseName(sPath) = $sStartup 'And Not bNoStartup
            sExportName = Project.GetExported(sPath)
            bExported = sExportName
            If bExported Then bExportedNamespace = sExportName <> File.BaseName(sPath)
          Endif

        Case "jpg", "jpeg", "xpm", "bmp", "png", "gif"

          If bLink Then
            Try hFile = Open sPath
            If Error Then
              iFileSize = 0
            Else
              iFileSize = Lof(hFile)
              Close hFile
            Endif
          Else
            iFileSize = .Size
          Endif

          If iFileSize > Project.MAX_ICON_SIZE Then
            sIcon = sPrefix &/ "image"
          Else
            sIcon = sPath
            bImage = True
          Endif

        Case "svg", "svgz"
          sIcon = sPrefix &/ "vector"

        Case "htm", "html"
          sIcon = sPrefix &/ "html"
          
        Case "js", "json", "css", "xml", "pdf"
          sIcon = sPrefix &/ sExt

        Case "txt", "text"
          sIcon = sPrefix &/ "text"

        Case "connection"
          If Exist(File.SetExt(sPath, "template")) Then bExported = True
          sIcon = sPrefix &/ "database"
          
        Case "console"
          sIcon = sPrefix &/ "terminal"
          
        Case Else
          Try hImage = DesktopMime.FromFile(sPath).GetIcon(iSize)
          If hImage Then
            sIcon = "//"
          Else
            sIcon = sPrefix &/ "file"
          Endif
          
      End Select

    Endif

  End With

  If Left$(sIcon) = "/" Then

    If Not hImage Then Try hImage = Image.Load(sIcon)
    
    If hImage Then

      fRapport = hImage.Width / hImage.Height
      If fRapport < 1 Then fRapport = 1 / fRapport

      If fRapport < 8 Then

        If bImage Then
          
          If hImage.H > (iSize * 2 - 4) Then 
            iSize *= 2
          Else If hImage.H > (iSize - 4) Then
            iSize = hImage.H + 4
          Endif
          
          iSize -= 4
          hThumb = hImage.Stretch(hImage.Width * iSize / hImage.Height, iSize)
          iSize += 4
          hImage = New Image(hThumb.W + 4, hThumb.H + 4, Color.Transparent)
          hImage.DrawImage(hThumb, 2, 2)
          Paint.Begin(hImage)
          Paint.DrawRect(0, 0, hImage.W, hImage.H, Color.Merge(Color.TextBackground, Color.LightForeground))
          Paint.End
          
        Else
          
          hImage = hImage.Stretch(hImage.Width * iSize / hImage.Height, iSize)

        Endif

        ' If Not bImage Then
        '   hImage = hImage.Stretch(hImage.Width * iSize / hImage.Height, iSize)
        ' Else
        '   iSize = 32
        '   If hImage.Height > iSize Then
        '     hImage = hImage.Stretch(hImage.Width * iSize / hImage.Height, iSize)
        '   Endif
        '   If hImage.Width > iSize Then
        '     hImage = hImage.Stretch(iSize, hImage.Height * iSize / hImage.Width)
        '   Endif
        ' Endif
        
      Else

        hImage = Null

      Endif

    Endif

    If Not hImage Then hImage = Picture[sPrefix &/ "image"].Image
    GoSub ADD_EMBLEM
    hPict = hImage.Picture

  Else

    sKey = File.Dir(sIcon) &/ File.BaseName(sIcon) & "-"
    If iSize Then 
      sKey &= CStr(iSize) & "-"
    Else
      sKey &= "16-"
    Endif
    
    If bLink Then sKey &= "l"
    
    If Not bNoEmblem Then
      'If bStartup Then sKey &= "s"
      If bConflict Then sKey &= "c"
      If bLocked Then sKey &= "k"
      If bAdded Then sKey &= "a"
      If bModified Then sKey &= "m"
      If bExported Then sKey &= If(bExportedNamespace, "n", "x")
      If bDoNotTranslate Then sKey &= "t"
      If bCompressed Then sKey &= "z"
      If bReadOnly Then sKey &= "r"
    Endif
    
    If Right(sKey) = "-" Then sKey = Left(sKey, -1)
    sKey &= ".png"

    'hPict = NULL
    'IF NOT bNoStartup THEN hPict = Picture[sKey]

    sCacheKey = "$" & Replace(sKey, "icon:/", "$/")
    Try hPict = Picture[sCacheKey]
    If Not hPict Then

      If sModule Then
        hImage = Project.MakeModuleIcon(sModule, iSize, bStartup And Not bNoEmblem)
      Else
        Try hImage = Picture[sIcon].Image
        If Error Then hImage = Picture[sPrefix &/ "file"].Image
      Endif
      'If Not hPict Then hPict = Picture[sPrefix &/ "file"]
      'If Not hPict Then hPict = Picture["img/16/unknown.png"]
      'hPict = hPict.Copy()

      If iSize Then hImage = hImage.Stretch(hImage.Width * iSize / hImage.Height, iSize)
      GoSub ADD_EMBLEM
      hPict = hImage.Picture
      Picture[sCacheKey] = hPict

    Endif

  Endif

  Return hPict

ADD_EMBLEM:

  If bLink Then hImage = AddLinkEmblem(hImage)
  If bCompressed Then hImage = AddCompressedEmblem(hImage)
  
  If Not bNoEmblem Then
    If bExported Then hImage = Project.AddEmblem(hImage, If(bExportedNamespace, "img/32/exported-namespace.png", "img/32/exported.png"), iEmblemSize)
    'If bCompressed Then hImage = Project.AddEmblem(hImage, "img/32/compressed.png", iEmblemSize)
    If bDoNotTranslate Then hImage = Project.AddEmblem(hImage, "img/32/do-not-translate.png", iEmblemSize)
    If bAdded Then hImage = Project.AddEmblem(hImage, "icon:/32/added", iEmblemSize)
    If bModified Then hImage = Project.AddEmblem(hImage, "icon:/32/modified", iEmblemSize)
    If bConflict Then hImage = Project.AddEmblem(hImage, "icon:/32/conflict", iEmblemSize)
    If bLocked Or If bReadOnly Then hImage = Project.AddEmblem(hImage, "icon:/32/lock", iEmblemSize)
  Endif
  
  Return

End

Private Function IsSourcePath(sPath As String) As Boolean

  Dim sExt As String

  If $sSourceDir <> $sDir Then Return sPath & "/" Begins $sSourceDir &/ "/"

  If Left(sPath) = "/" Then
    If File.Dir(sPath) <> $sSourceDir Then Return
  Endif 
  
  sExt = File.Ext(sPath)
  If CModule.Ext.Exist(sExt) Then Return True

End

Public Sub TreeView_Expand()
  
  Dim sKey As String 
  Try sKey = $hTree.Item.Key
  If Not sKey Then Return
  If Left(sKey) = "$" Then Return
  FillDir(sKey)

End

Public Sub TreeView_Collapse()
  
  Dim sKey As String = $hTree.Item.Key
  
  $bHasJustCollapsed = True
  If Not sKey Then Return
  If File.Dir(sKey) = $sDir And If Not IsDir(sKey) Then $hTree[sKey].Expanded = True
  
End

Public Sub TreeView_Select()
  
  'RefreshMenu
  
  If $hTree.Current And If $hTree.Current.Children Then 
    ' Print Mouse.X ;; Mouse.Y ' generates crashes on random places, strange...
    'If Not tvwProject.Find(Mouse.X, Mouse.Y) Then
    $bExpandOnMouseUp = True
    'Endif
  Endif
  
End

Public Sub TreeView_MouseUp()
  
  If $bExpandOnMouseUp And If Not $bHasJustCollapsed Then
    Try $hTree.Current.Expanded = True
  Endif
  $bExpandOnMouseUp = False
  $bHasJustCollapsed = False
  
End

    
Public Sub FillDir(sDir As String)

  If Not sDir Then Return
  If Left(sDir) = "$" Then Return
  If Not IsDir(sDir) Then Return

  $hTree.MoveTo(sDir)
  If $hTree.MoveChild() Then Return
  If $hTree.Item.Key <> sDir & "/" Then Return

  Inc Application.Busy  
  $hTree[sDir & "/"].Delete
  AddDir(sDir)
  Dec Application.Busy
  
Catch

  Debug Error.Where; ": "; Error.Text
  Dec Application.Busy
  
End

Public Sub EnsureKey(sKey As String)

  Dim sPath As String
  Dim sElt As String

  If Not $hTree Then Return
  
  sPath = "/"
  For Each sElt In Split(sKey, "/")
    sPath &/= sElt
    If $hTree.Exist(sPath) Then FillDir(sPath)
  Next

End

Public Sub SelectKey(sKey As String)

  Dim hModule As CModule
  Dim sBase As String

  If Not $hTree Then Return

  EnsureKey(sKey)

  If Not $hTree.Exist(sKey) Then
    If Right$(sKey, 6) = ".class" Then
      sBase = Left$(sKey, -6)
      For Each hModule In CModule.All
        sKey = sBase & "." & hModule.Key
        If $hTree.Exist(sKey) Then Break
      Next
    Endif
  Endif

  Try $hTree[sKey].Selected = True
  Try $hTree[sKey].EnsureVisible

End

Public Sub FindForm(sClass As String) As String

  Dim sFamily As String
  Dim sPath As String

  For Each sFamily In CFamily.List
    sPath = Sources[sClass & "." & sFamily]
    If Exist(sPath) Then Return sPath
  Next

End

Public Function ExistForm(sClass As String) As Boolean

  Dim sFamily As String

  For Each sFamily In CFamily.List
    If Sources.Exist(sClass & "." & sFamily) Then Return True
  Next

End

Public Function HasForm(sPath As String) As Boolean
  
  If File.Ext(sPath) <> "class" Then Return

  Return ExistForm(File.BaseName(sPath))

End

Public Sub HasTest() As Boolean

  Dim sPath As String
  
  For Each sPath In Sources
    If File.Ext(sPath) = "test" Then Return True
  Next
  
End

Public Sub GetTestModules() As String[]
  
  Dim sPath As String
  Dim aResult As New String[]
  
  For Each sPath In Sources
    If File.Ext(sPath) = "test" Then aResult.Add(sPath)
  Next
  
  aResult.Sort(gb.Natural + gb.IgnoreCase)
  Return aResult
  
End


Private Sub RefreshInheritance()

  Dim sKey As String

  $aInheritance.Clear

  For Each sKey In Sources
    If Not File.Ext(sKey) = "class" Then Continue
    Try $hTree[sKey].Delete
    $aInheritance.Add(sKey)
  Next

  DoRefreshAfter

End

Private Sub GetOpenedItems(sParent As String) As String[]

  Dim iLevel As Integer
  Dim aOpen As String[]
  
  aOpen = New String[]

  $hTree.MoveTo(sParent)
  
  Do
    If Not $hTree.Available Then
      Dec iLevel
      If iLevel <= 0 Then Break
      $hTree.MoveParent
      $hTree.MoveNext
    Else If $hTree.Item.Expanded Then
      aOpen.Add($hTree.Item.Key)
      Inc iLevel
      $hTree.MoveChild()
    Else
      $hTree.MoveNext
    Endif
  Loop
  
  Return aOpen

End

Public Sub RefreshKey(sKey As String, Optional sOld As String)

  Dim aOpen As String[]
  Dim sOpen As String
  
  If Not $hTree Then Return
  If Not sKey Then Return
  
  If Not $hTree.Exist(sKey) And If Not Exist(sKey) And If IsSourcePath(sKey) Then
    sKey = FindForm(File.BaseName(sKey))
  Endif

  If Not Exist(sKey) Then 
    Try $hTree.Remove(sKey)
    Return
  Endif

  If Not sOld Or If sOld = sKey Then 
  
    EnsureKey(sKey)
    AddFile(File.Dir(sKey), File.Name(sKey), True)
 
  Else

    aOpen = GetOpenedItems(sOld)
    
    EnsureKey(sOld)
    Try $hTree[sOld].Delete
    If Error Then Return
  
    If IsSourcePath(sKey) And If File.Ext(sKey) = "class" And If Not ExistForm(File.BaseName(sKey)) Then
      RefreshInheritance
    Else
      If AddFile(File.Dir(sKey), File.Name(sKey), True) Then AddDir(sKey)
      If Exist(sOld) Then
        If AddFile(File.Dir(sOld), File.Name(sOld), True) Then AddDir(sOld)
      Endif
    Endif
    
    For Each sOpen In aOpen
      If sOpen Begins sOld Then $hTree[sKey & Mid$(sOpen, Len(sOld) + 1)].Expanded = True
    Next
    
  Endif
  
End

Public Sub AddExtern(sPath As String)
  
  $cExtern[sPath] = True
  
End

Public Sub ExistClass(sClass As String) As Boolean

  If Sources.Exist(sClass & ".class") Then Return True
  If Sources.Exist(sClass & ".module") Then Return True
  If Sources.Exist(sClass & ".test") Then Return True

End

Public Sub DefineStartup(sClass As String) As String
  
  If sClass <> $sStartup Then 
    Swap $sStartup, sClass
    RefreshKey(FindKey(sClass))
    RefreshKey(FindKey($sStartup))
  Endif
  
  Return $sStartup
  
End

Private Sub AddLinkEmblem(hSource As Image) As Image

  Dim hImage As Image
  Dim iSize As Integer

  iSize = Min(hSource.H, hSource.W) \ 2
  
  hImage = hSource.Copy()
  
  Paint.Begin(hImage)
  
  Paint.MoveTo(hImage.W, hImage.H)
  Paint.RelLineTo(0, -iSize)
  Paint.RelLineTo(-iSize, iSize)
  Paint.RelLineTo(iSize, 0)
  Paint.Background = Color.Black
  'Paint.Background = Color.SetAlpha(Color.TextForeground, 64)
  Paint.Operator = Paint.OperatorDestOut
  Paint.Fill
  
  iSize *= 0.85
  
  Paint.MoveTo(hImage.W, hImage.H)
  Paint.RelLineTo(0, -iSize)
  Paint.RelLineTo(-iSize, iSize)
  Paint.RelLineTo(iSize, 0)
  Paint.Background = Color.SetAlpha(Color.TextForeground, 64)
  Paint.Operator = Paint.OperatorOver
  Paint.Fill
  
  Paint.End
  Return hImage

End

Private Sub AddCompressedEmblem(hSource As Image) As Image

  Dim hImage As Image
  
  hImage = hSource.Copy()
  If Application.DarkTheme Then
    hImage.Brightness(0.5)
  Else
    hImage.Brightness(-0.25)
  Endif

  Return hImage

End

  Public Sub AddOtherProject(sDir As String)
  
  sDir = File.RealPath(sDir)
  If $aOtherProjects.Exist(sDir) Then Return
  $aOtherProjects.Add(sDir)
  Refresh
  
End

Public Sub ClearOtherProjects()
  
  If Not $aOtherProjects Or If $aOtherProjects.Count = 0 Then Return 
  $aOtherProjects.Clear
  Refresh
  
End

Private Sub IsClassFromOtherProject(sPath As String) As Boolean

  Dim iPos As Integer
  
  iPos = RInStr(sPath, "/.src/")
  If iPos = 0 Then Return
  Return $aOtherProjects.Exist(Left(sPath, iPos - 1))
  
End
