' Gambas class file

Static Private $sLang As String

Static Public Function Run() As String

  FNewTranslation.ShowModal
  Return $sLang

End

Public Sub Form_Open()
  
  Dim sLang As String
  
  cvwLanguage.Columns[0].Text = ("Language")
  cvwLanguage.Columns[0].Expand = True
  cvwLanguage.AddColumn(("File"), Desktop.Scale * 12)
  
  For Each sLang In MyLanguage.GetAll()
    If Project.HasTranslation(sLang) Then Continue
    With cvwLanguage.Add(sLang, MyLanguage.ToName(sLang))
      .[1] = sLang & ".po"
      .Picture = Picture["flag:/small/" &/ Language[sLang].Country]
    End With
  Next
  
  btnOK.Enabled = False

End


Public Sub btnOK_Click()

  $sLang = cvwLanguage.Key
  Me.Close

End

Public Sub btnCancel_Click()

  $sLang = ""
  Me.Close

End


Public Sub cvwLanguage_Activate()

  btnOK.Value = True

End

Public Sub cvwLanguage_Select()

  btnOK.Enabled = Not IsNull(cvwLanguage.Current)

End
