' Gambas class file

Static Public ColorName As String
Static Public Value As Integer
Static Public Pattern As String

Static Private $bInsert As Boolean
Static Private $sColor As String
Static Private $bAlpha As Boolean
'Static Private $aDefaultColors As String[] = ["Background", "Foreground", "SelectedBackground", "SelectedForeground", "LightBackground", "LightForeground", "TextBackground", "TextForeground", "ButtonBackground", "ButtonForeground", "TooltipBackground", "TooltipForeground", "LinkForeground", "VisitedForeground"]
Static Private $aColorName As String[]
Static Private $aPict As Picture[]
Static Private $sTitle As String
Static Private $aPattern As String[]

Private Const DEFAULT_COLOR As String = "-"

Private $aButtonPattern As Button[]

Static Private Sub GetColor(sName As String) As Integer
  
  Dim hClass As CClassInfo
  Dim hSym As CSymbolInfo
  
  hClass = Project.Documentation.Classes["Color"]
  hSym = hClass.Symbols[sName]
  If hSym.Kind = "C" And If hSym.Type = "i" Then Return CInt(hSym.Value)
  Try Return Object.GetProperty(Classes["Color"], sName)
  Return -1
  
End

Static Public Sub FromString(sColor As String) As Integer
  
  If sColor == "None" Then Return -1
  If IsLetter(Left(sColor)) Then Return GetColor(sColor)
  If Left(sColor) = "#" Then Return Val("&H" & Mid$(sColor, 2))
  If Left(sColor) = "&" Then Return Val(sColor)
  
Catch
  
  Return -1
  
End

Static Public Function Run(Optional sColor As String, Optional bAlpha As Boolean, Optional sTitle As String, Optional aPrefix As String[]) As Boolean

  $bInsert = False
  $sColor = sColor
  $bAlpha = bAlpha
  $sTitle = sTitle
  $aPattern = aPrefix

  Return Not FColorChooser.ShowModal() 

End

Static Public Sub Insert(bAlpha As Boolean, sTitle As String, aPrefix As String[], Optional sColor As String) As Boolean
  
  $bInsert = True
  $sColor = sColor
  $bAlpha = bAlpha
  $sTitle = sTitle
  $aPattern = aPrefix

  Return Not FColorChooser.ShowModal() 
  
End

Public Sub btnOK_Click()

  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub _new()

  gvwColor.AddColumn("", Desktop.Scale * 4, Align.Center)
  gvwColor.AddColumn(("Color")).Expand = True
  gvwColor.AddColumn(("R"), Desktop.Scale * 6, Align.Center)
  gvwColor.AddColumn(("G"), Desktop.Scale * 6, Align.Center)
  gvwColor.AddColumn(("B"), Desktop.Scale * 6, Align.Center)
  
End

Public Sub Form_Open()

  Dim hClass As CClassInfo
  Dim hSym As CSymbolInfo
  Dim sColor As String
  Dim iIndex As Integer
  Dim I As Integer
  Dim aColor As String[]
  Dim iSep As Integer
  
  Settings.Read(Me)

  If $aPattern And If $aPattern.Count Then
    
    $aButtonPattern = New Button[$aPattern.Count]
    For I = 0 To $aPattern.Max
      $aButtonPattern[I] = New Button(panColor) As "Color"
      $aButtonPattern[I].AutoResize = True
      '$aButtonPattern[I].H = Desktop.Scale * 4
      Project.SetEditorFont($aButtonPattern[I])
    Next
    
    btnOK.Hide
    sprColor.Raise
    btnCancel.Raise
    
  Endif

  hClass = Project.Documentation.Classes["Color"]
  If hClass Then

    $aColorName = ["", "Transparent", "-"]
    
    aColor = New String[]
    For Each hSym In hClass.Symbols
      sColor = hSym.Name
      If sColor = "Default" Or If hSym.Kind <> "R" Or If hSym.Type <> "i" Or If aColor.Exist(sColor) Then Continue
      aColor.Add(sColor)
    Next
    aColor.Sort(gb.Natural)
    $aColorName.Insert(aColor)
    
    If aColor.Count Then
      iSep = $aColorName.Count
      $aColorName.Add("-")
    Endif

    aColor = New String[]
    For Each hSym In hClass.Symbols
      sColor = hSym.Name
      If sColor = "Default" Or If hSym.Kind <> "C" Or If hSym.Type <> "i" Or If $aColorName.Exist(sColor) Or If aColor.Exist(sColor) Then Continue
      aColor.Add(sColor)
    Next
    aColor.Sort(gb.Natural)
    
    $aColorName.Insert(aColor)

    $aPict = New Picture[$aColorName.Count]
  
    gvwColor.Rows.Count = $aColorName.Count
    gvwColor.Rows.H = Desktop.Scale * 4
    gvwColor.Rows[2].H = 1
    gvwColor[2, 0].ColumnSpan = gvwColor.Columns.Count
    If iSep Then
      gvwColor.Rows[iSep].H = 1
      gvwColor[iSep, 0].ColumnSpan = gvwColor.Columns.Count
    Endif
    
  Endif

  If Not IsNull(Val($sColor)) Then
    gvwColor.Row = 0
    dlgColor.SelectedColor = Val($sColor)
    iIndex = 1
  Else
    If $sColor Then
      gvwColor.Row = $aColorName.Find($sColor)
    Else
      gvwColor.Row = 0
    Endif
    iIndex = 0
  Endif
  
  If Not hClass Then
    iIndex = 1
    tabColor[0].Visible = False
    tabColor.ShowTabBar = False
    tabColor.Border = False
    panFreeColor.Margin = False
  Else
    tabColor[0].Visible = True
    tabColor.ShowTabBar = True
    tabColor.Border = True
    panFreeColor.Margin = True
  Endif
  
  tabColor.Index = iIndex
  
  dlgColor.ShowAlpha = $bAlpha
  
  If $sTitle Then
    Me.Title = $sTitle
  Else
    $sTitle = ("Select a color")
  Endif
  
  UpdateColor

End


Public Sub dlgColor_Activate()

  If $aPattern Then
    $aButtonPattern[0].Value = True
  Else
    btnOK.Value = True
  Endif

End


Public Sub Form_Close()

  Dim hButton As Button
  
  If $aPattern Then
    
    For Each hButton In $aButtonPattern
      hButton.Delete
    Next
    
    $aButtonPattern = Null
    
    btnOK.Show
    
  Endif

  Settings.Write(Me)  

End

Private Sub UpdateColor()

  Dim sColor As String
  Dim I As Integer

  If tabColor.Index = 0 Then
    Try sColor = $aColorName[gvwColor.Row]
  Else
    sColor = CStr(dlgColor.Value)
  Endif
  
  If sColor = "-" Then Return

  If Not sColor Then
    ColorName = ""
    Value = -1
  Else If IsLetter(Left(sColor)) Then
    ColorName = sColor
    Value = GetColor(sColor)
  Else
    Value = Val(sColor)
    If $bAlpha Then
      ColorName = "&H" & Hex$(Value, 8)
    Else
      ColorName = "&H" & Hex$(Value, 6)
    Endif
  Endif
  
  If tabColor.Index = 0 Then
    If Value >= 0 Then dlgColor.Value = Value
  Endif

  If $aPattern Then
    
    For I = 0 To $aPattern.Max
      sColor = CInsertColor.FormatColor(Value, $aPattern[I])
      $aButtonPattern[I].Text = Replace(sColor, "&", "&&")
      $aButtonPattern[I].Tag = sColor
    Next

    'Me.Width = Max(Me.Width, $aButtonPattern[$aPattern.Max].X + $aButtonPattern[$aPattern.Max].W + Desktop.Scale * 40)

  Endif

End

Public Sub gvwColor_Select()

  UpdateColor

End

Public Sub dlgColor_Change()

  UpdateColor

End

Public Sub tabColor_Click()

  UpdateColor

End

Public Sub gvwColor_Draw(X As Integer, Y As Integer, Width As Integer, Height As Integer, Row As Integer, (Column) As Integer)
  
  If $aColorName[Row] = "-" Then Paint.FillRect(X, Y, Width, Height, Color.LightForeground)
  
End

Public Sub gvwColor_Data(Row As Integer, Column As Integer)

  Dim hPict As Picture
  Dim hImage As Image
  Dim S As Integer
  Dim sName As String
  
  sName = $aColorName[Row]
  If sName = "-" Then Return
  
  With gvwColor.Data

    Select Case Column
      
      Case 0
        If Row > 0 Then
          hPict = $aPict[Row]
          If Not hPict Then
            S = Stock.GetSize("small")
            hImage = Picture["img/32/tile.png"].Image
            Paint.Begin(hImage)
            Paint.DrawRect(0, 0, 32, 32, Color.TextForeground, 2)
            Paint.FillRect(2, 2, 28, 28, GetColor(sName))
            Paint.End
            hPict = hImage.Stretch(S, S).Picture
            $aPict[Row] = hPict
          Endif
          .Picture = hPict
        Endif
      
      Case 1
        If Row = 0 Then
          .Text = " Default"
        Else
          .Text = " " & $aColorName[Row]
        Endif
        
      Case 2
        If Row > 0 And If sName <> "Transparent" Then .Text = CStr(Color[GetColor(sName)].Red)
        
      Case 3
        If Row > 0 And If sName <> "Transparent" Then .Text = CStr(Color[GetColor(sName)].Green)
        
      Case 4
        If Row > 0 And If sName <> "Transparent" Then .Text = CStr(Color[GetColor(sName)].Blue)
        
    End Select
    
  End With

End

Public Sub gvwColor_Activate()

  If $aButtonPattern And If $aButtonPattern.Count Then
    $aButtonPattern[0].Value = True
  Else
    btnOK.Value = True
  Endif

End

Public Sub Color_Click()
  
  Pattern = Last.Tag
  Me.Close(True)
  
End
